untyped
global function VTOL_Init
global function PlayerUsingVTOL
global function VTOLIntro	// debug, dont forget remove it

vector VTOL_CENTER = < 0, 0, 3000 >

void function VTOL_Init()
{
	AddCallback_OnClientConnected( OnClientConnected )

	if( GetMapName() == "mp_grave" )
		VTOL_CENTER = < 0, 0, 3000 >
}

void function OnClientConnected( entity player )
{
	player.s.usingVTOL <- false
	player.s.VTOLIntroDone <- false
}

void function PlayerUsingVTOL( entity owner )
{
	owner.EndSignal( "OnDestroy" )
	owner.EndSignal( "OnDeath" )
	owner.s.usingVTOL = true
	owner.s.DisableGUI = true
	owner.kv.VisibilityFlags = 0
	PhaseShift( owner, 0, 1.2 )
	HolsterAndDisableWeapons( owner )
	owner.SetOrigin( owner.GetOrigin() )
	FindNearestSafeSpotAndPutEntity( owner )
	owner.FreezeControlsOnServer()
	owner.SetInvulnerable()
	vector beforeOrigin = owner.GetOrigin()

	OnThreadEnd(
		function() : ( owner, beforeOrigin )
		{
			if( !IsValid( owner ) )
				return
			Remote_CallFunction_NonReplay( owner, "ServerCallback_ResetMapSettings" )
			owner.s.usingVTOL = false
			owner.s.DisableGUI = false
			owner.kv.VisibilityFlags = ENTITY_VISIBLE_TO_EVERYONE
			owner.ClearInvulnerable()
			DeployAndEnableWeapons( owner )
			owner.UnfreezeControlsOnServer()
			Remote_CallFunction_Replay( owner, "ServerCallback_TitanEMP", 0.4, 0.4, 0.4 )
			ScreenFade( owner, 0, 0, 0, 255, 2, 0.2, (FFADE_IN | FFADE_PURGE) )

			if( !IsAlive( owner ) )
				return
			owner.SetOrigin( beforeOrigin )
			FindNearestSafeSpotAndPutEntity( owner )
			PlayFXOnEntity( $"P_phase_shift_main", owner )
			EmitSoundOnEntityOnlyToPlayer( owner, owner, "Pilot_PhaseShift_WarningToEnd_1P" )
			EmitSoundOnEntityExceptToPlayer( owner, owner, "Pilot_PhaseShift_WarningToEnd_3P" )
		}
	)

	ScreenFadeToBlack( owner, 1.0, 0.8 )
	wait 1.0
	WaitFrame()
	thread VTOLIntro( owner )

	entity evacSpaceNode = GetEvacSpaceNode()
	if( !IsValid( evacSpaceNode ) )
		evacSpaceNode = GetEnt( "spaceNode" )
	vector validOrigin = evacSpaceNode.GetOrigin()
	validOrigin.z += 500
	for( ;; )
	{
		WaitFrame()
		if( !owner.s.usingVTOL )
			return

		owner.SetOrigin( validOrigin )
		owner.SetVelocity( < 0, 0, 0 > )
		if( owner.s.VTOLIntroDone )
			Remote_CallFunction_NonReplay( owner, "ServerCallback_SetMapSettings", 1.0, false, null, null, null, null, null, 0.0, 0.5 )
	}
}

void function VTOLIntro( entity owner )
{
	owner.EndSignal( "OnDestroy" )
	owner.EndSignal( "OnDeath" )
	vector circle = < 0, RandomInt( 360 ), 0 >
	vector center = VTOL_CENTER
	vector shipAng = AnglesToRight( circle )
	vector circleDir = AnglesToForward( circle )
	vector newPos = center + circleDir * 1000
	entity mover = CreateScriptMover( newPos, AnglesToRight( circle ) )

	entity gunship = CreateDropship( owner.GetTeam(), newPos, shipAng )
	asset shipMdl = owner.GetTeam() == TEAM_IMC ? $"models/vehicle/straton/straton_imc_gunship_01.mdl" : $"models/vehicle/hornet/hornet_fighter.mdl"
	gunship.SetValueForModelKey( shipMdl )
	gunship.SetMaxHealth( 15000 )
	gunship.SetHealth( 15000 )
	gunship.SetModel( shipMdl )

	gunship.EndSignal( "OnDestroy" )

	DispatchSpawn( gunship )

	// setup visual
	SetDefaultMPEnemyHighlight( gunship )

	// fly in
	thread PlayAnim( gunship, "st_AngelCity_IMC_Win_ComeIn" ) // flying anim
	// warp effect
	thread WarpInEffectGunship( gunship )
    WaittillAnimDone( gunship )

	gunship.SetParent( mover, "", true )
	for( ;; )
	{
		WaitFrame()
		circle.y -= 1
		shipAng = AnglesToRight( circle )
		circleDir = AnglesToForward( circle )
		newPos = center + circleDir * 1000
		mover.NonPhysicsMoveTo( newPos, 0.1, 0.0, 0.0 )
		mover.NonPhysicsRotateTo( shipAng, 0.1, 0.0, 0.0 )
	}

}

void function WarpInEffectGunship( entity gunship )
{
	gunship.EndSignal( "OnDestroy" )
	gunship.Hide() // this still shows it's lights
	//gunship.kv.VisibilityFlags = ENTITY_VISIBLE_TO_NOBODY

	float sfxWait = 0.1
	float totalTime = WARPINFXTIME
	float preWaitTime = 0.16 // give it some time so it's actually playing anim, and we can get it's "origin" attatch
	string sfx = "dropship_warpin"

	wait preWaitTime

	int attach = gunship.LookupAttachment( "origin" )
	vector origin = gunship.GetAttachmentOrigin( attach )
	vector angles = gunship.GetAttachmentAngles( attach )

	gunship.Show()
	//gunship.kv.VisibilityFlags = ENTITY_VISIBLE_TO_EVERYONE
	entity fx = PlayFX( FX_GUNSHIP_CRASH_EXPLOSION_ENTRANCE, origin, angles )
	fx.FXEnableRenderAlways()
	fx.DisableHibernation()

	wait sfxWait
	EmitSoundAtPosition( TEAM_UNASSIGNED, origin, sfx )

	wait totalTime - sfxWait
}





