global function BetterRespawnPoint_Init
global function GetsBetterSpawnPointByTeam
global function BetterRespawnPointEnable
global function SetBetterRespawnPointEnable

bool BETTER_RESPAWN_POINT_ENABLE = true

void function BetterRespawnPoint_Init()
{
	AddCallback_OnPlayerRespawned( BetterRespawnPoint_Pilot )
	SetRecalculateRespawnAsTitanStartPointCallback( BetterRespawnPoint_Titan )
}

bool function BetterRespawnPointEnable()
{
	return BETTER_RESPAWN_POINT_ENABLE
}

void function SetBetterRespawnPointEnable( bool state )
{
	BETTER_RESPAWN_POINT_ENABLE = state
}

void function BetterRespawnPoint_Pilot( entity player )
{
	if( !BetterRespawnPointEnable() )
		return
	if ( GetGameState() < eGameState.Playing )
		return
	if( player.IsTitan() )
		return

	entity point = GetsBetterSpawnPointByTeam( player.GetTeam(), false, 2000 )
	player.SetOrigin( point.GetOrigin() )
	player.SetAngles( point.GetAngles() )
}

entity function BetterRespawnPoint_Titan( entity player, entity spawnPoint )
{
	if( !BetterRespawnPointEnable() )
		return spawnPoint

	return GetsBetterSpawnPointByTeam( player.GetTeam(), true, 2000 )
}

entity function GetsBetterSpawnPointByTeam( int team, bool isTitan, float distance, int callTimes = 0 )
{
	int i = 0
	int nearPlayer = 0
	int nearPlayerSave = 0
	float totalDistance = 0
	float distanceSave = 0
	int arrayNum = -1
	array<entity> points = isTitan ? SpawnPoints_GetTitan() : SpawnPoints_GetPilot()

	if( callTimes >= 10 || GetPlayerArrayOfTeam( team ).len() <= 1 )
		return points[ RandomInt( points.len() ) ]

	foreach( point in points )
	{
		foreach( ent in GetPlayerArray() )
		{
			if( !IsAlive( ent ) )
				continue
			if( ent.GetTeam() != team )
			{
				if( Distance( ent.GetOrigin(), point.GetOrigin() ) <= distance / 2 )
					totalDistance += distance * 2
				else
					totalDistance -= distance / 2
				continue
			}
			if( Distance( ent.GetOrigin(), point.GetOrigin() ) <= distance )
			{
				nearPlayer++
				totalDistance += Distance( ent.GetOrigin(), point.GetOrigin() )
			}
		}

		if( nearPlayer > nearPlayerSave || ( nearPlayer == nearPlayerSave && totalDistance < distanceSave ) )
		{
			nearPlayerSave = nearPlayer
			distanceSave = totalDistance
			arrayNum = i
		}

		i++
		nearPlayer = 0
		totalDistance = 0
	}

	if( arrayNum < 0 || distance < 500 )
		return GetsBetterSpawnPointByTeam( team, isTitan, distance + 500, callTimes + 1 )

	return points[ arrayNum ]
}

