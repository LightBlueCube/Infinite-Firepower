untyped
global function HudMessagePriority_Init
global function SendHudMessageWithPriority

/*

KsGUI: 100 / OnFadeOut: 90
NuclearBomb: 90.01
PilotShieldDamaged: 93 / OnFadeOut: 91
SkillUsingMessage: 92 / OnFaild: 101 / OnFadeOut: 92
LoadoutTips: 91
AntiAFK: 95
RandMap: 102
AntiInsult: 102

*/


void function HudMessagePriority_Init()
{
	RegisterSignal( "SendingNewHudMessage" )
	AddCallback_OnClientConnected( OnClientConnected )
}

void function OnClientConnected( entity player )
{
	player.s.nowMsgPriority <- -1.0
}

bool function SendHudMessageWithPriority( entity player, float priority, var text, float yaw, float pos, vector rgb, vector time, float priorityOnFadeout = 90 )
{
	if( priority < player.s.nowMsgPriority )
		return false
	player.s.nowMsgPriority = priority
	string str = string( text )
	thread StartSendHudMessage( player, str, yaw, pos, rgb, time, priorityOnFadeout )
	return true
}

void function StartSendHudMessage( entity player, string text, float yaw, float pos, vector rgb, vector time, float priorityOnFadeout )
{
	player.EndSignal( "OnDestroy" )
	player.Signal( "SendingNewHudMessage" )
	player.EndSignal( "SendingNewHudMessage" )

	//fade in
	int tick = int( time.x * 10 )
	vector rgbFadeStart = < 0, 0, 0 >
	vector fadePerTick = < rgb.x / tick, rgb.y / tick, rgb.z / tick >
	WhileSendHudMessage( player, text, yaw, pos, rgbFadeStart, fadePerTick, tick )

	// show msg
	tick = int( time.y * 10 )
	fadePerTick = < 0, 0, 0 >	// dont do any fade
	WhileSendHudMessage( player, text, yaw, pos, rgb, fadePerTick, tick )

	player.s.nowMsgPriority = priorityOnFadeout

	// fade out
	tick = int( time.z * 10 )
	rgbFadeStart = rgb
	fadePerTick = < rgb.x / tick, rgb.y / tick, rgb.z / tick >
	fadePerTick *= -1	// cuz we r doing fade out
	WhileSendHudMessage( player, text, yaw, pos, rgb, fadePerTick, tick )

	player.s.nowMsgPriority = -1.0
}

void function WhileSendHudMessage( entity player, string text, float yaw, float pos, vector rgb, vector fadePerTick, int tick )
{
	for( int i = tick; i > 0; i-- )
	{
		rgb += fadePerTick
		SendHudMessage( player, text, yaw, pos, int( rgb.x ), int( rgb.y ), int( rgb.z ), 255, 0, 0.2, 0.0 )
		WaitFrame()
	}
}