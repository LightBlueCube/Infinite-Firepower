global function TryFindBestPoint
global function TryFindBestPointWithSettings
global function BetterRespawnPointEnable
global function SetBetterRespawnPointEnable

struct{
	bool betterRespawnPointEnable = true
}file

bool function BetterRespawnPointEnable()
{
	return file.betterRespawnPointEnable
}

void function SetBetterRespawnPointEnable( bool state )
{
	file.betterRespawnPointEnable = state
}

entity function TryFindBestPoint( int team, array<entity> points, float range = 2000 )
{
	array<table> svmTrainData
	array<vector> friendlyPos
	array<vector> enemyPos
	bool hasAlivePlayer = false
	foreach( player in GetPlayerArray() )
	{
		if( !IsAlive( player ) )
			continue
		hasAlivePlayer = true
		svmTrainData.append({
			pos = player.GetOrigin()
			team = player.GetTeam()
		})
		if( player.GetTeam() == team )
			friendlyPos.append( player.GetOrigin() )
		else
			enemyPos.append( player.GetOrigin() )
	}
	var svmUserdata = NSSvmTrain( svmTrainData )
	if( !hasAlivePlayer || GetPlayerArray().len() <= 1 )
		return points[ RandomInt( points.len() ) ]
	return TryFindBestPointWithSettings( team, points, range, svmUserdata, friendlyPos, enemyPos )
}

entity function TryFindBestPointWithSettings( int team, array<entity> points, float range, var svmUserdata, array<vector> friendlyPos, array<vector> enemyPos )
{
	array<entity> bestPoints = points
	float weightSave = 0
	foreach( point in points )
	{
		vector pointPos = point.GetOrigin()
		bool svmPredictSameTeam = NSSvmPredict( svmUserdata, pointPos ) == team
		bool haveFriendly = false
		bool haveEnemy = false
		float weight = 0

		foreach( pos in enemyPos )
		{
			float distance = Distance( pos, pointPos )
			if( distance > range )
				continue
			if( distance <= range * 0.5 )
				weight += -4
			else if( distance <= range )
			{
				if( haveEnemy )
				{
					weight += -1
					continue
				}

				haveEnemy = true
				weight += 1.9
				weight += GraphCapped( distance, range * 0.5, range, 0, 0.1 )
			}
		}
		foreach( pos in friendlyPos )
		{
			float distance = Distance( pos, pointPos )
			if( distance > range )
				continue
			haveFriendly = true
			weight += 1
			weight += GraphCapped( distance, range, 0, 0, 0.1 )
		}
		if( svmPredictSameTeam )
			weight += 2.5
		if( svmPredictSameTeam && haveEnemy )
			weight += -1.5
		if( !haveEnemy && !haveFriendly )
		{
			if( svmPredictSameTeam )
				weight += -1
			weight += -1
		}

		if( weight > weightSave )
		{
			weightSave = weight
			bestPoints = [ point ]
		}
		else if( weight == weightSave )
			bestPoints.append( point )
	}
	return bestPoints[ RandomInt( bestPoints.len() ) ]
}