global function VoteCommand_Init
global function StartNewVoteEvent
global function EndVoteEvent
global function GetVoteEvent
global function Vote_EasyReleaseFunc
global function Vote_EasyVoteFunc
global function Vote_EasyEndFunc

global enum eVoteEndCode
{
	invalid,
	timeout,
	complete
}

global struct VoteEventStruct
{
	void functionref( VoteEventStruct ) voteReleaseFunc	// = Vote_EasyReleaseFunc
	bool functionref( VoteEventStruct, entity, bool ) onVoteFunc	// = Vote_EasyVoteFunc
	void functionref( VoteEventStruct ) voteEndFunc	// = Vote_EasyEndFunc
	void functionref( bool ) callbackFunc
	string voteName = "新投票"
	float endTime = 0.0

	array<string> hasVotedPlayers = []
	int endCode = eVoteEndCode.invalid
	int voteYesCount = 0
	int voteNoCount = 0
	int id = 0
	table s
}

struct{
	table<int,VoteEventStruct> events
}file

const string NO_VOTE_TEXT = "当前无投票"

void function VoteCommand_Init()
{
	AddClientCommandCallback( "vote", VoteCommand )
	AddChatCommandCallback( "!vote", VoteCommand )
	AddChatCommandCallback( "！vote", VoteCommand )
}

bool function VoteCommand( entity player, array<string> args )
{
	if( args.len() == 0 )
	{
		Vote_ShowList( player )
		return true
	}
	if( args.len() <= 1 )
	{
		Chat_ServerPrivateMessage( player, "\x1b[31m指令缺少参数！", false, false )
		Vote_ShowList( player )
		return true
	}

	int id = args[0].tointeger()
	if( !( id in file.events ) )
	{
		Chat_ServerPrivateMessage( player, "\x1b[31m无效投票ID", false, false )
		Vote_ShowList( player )
		return true
	}
	if( ![ "y", "n" ].contains( args[1] ) )
	{
		Chat_ServerPrivateMessage( player, "\x1b[31m无效投票选项，请注意大小写！", false, false )
		Vote_ShowList( player )
		return true
	}
	bool vote = args[1] == "y" ? true : false

	foreach( k, v in file.events )
	{
		if( k != id )
			continue
		return v.onVoteFunc( v, player, vote )
	}
	Chat_ServerPrivateMessage( player, "\x1b[31m无效投票ID", false, false )
	Vote_ShowList( player )
	return true
}

void function Vote_ShowList( entity player )
{
	if( file.events.len() == 0 )
	{
		Chat_ServerPrivateMessage( player, "\x1b[31m当前无投票", false, false )
		return
	}

	Chat_ServerPrivateMessage( player, "\x1b[92m==== 当前活跃投票 ====", false, false )
	foreach( k, v in file.events )
	{
		Chat_ServerPrivateMessage( player, "\x1b[32m投票ID: \x1b[92m"+ k +"\x1b[32m | 投票描述: \x1b[92m"+ v.voteName, false, false )
	}
	Chat_ServerPrivateMessage( player, "\x1b[32m输入 \x1b[36m!vote 投票ID y \x1b[32m投同意票\n输入 \x1b[36m!vote 投票ID n \x1b[32m投反对票", false, false )
}

int function StartNewVoteEvent( VoteEventStruct event )
{
	event.id = GetValidEventId()
	file.events[ event.id ] <- event
	event.voteReleaseFunc( event )
	thread WaitForVoteEnd( event )
	return event.id
}

int function GetValidEventId()
{
	int i = 1
	while( i in file.events )
		i++
	return i
}

void function WaitForVoteEnd( VoteEventStruct event )
{
	while( Time() < event.endTime )
		WaitFrame()
	if( event.endCode == eVoteEndCode.invalid )
		event.endCode = eVoteEndCode.timeout
	event.voteEndFunc( event )
	delete file.events[ event.id ]
}

void function EndVoteEvent( VoteEventStruct event, int endCode )
{
	event.endCode = endCode
	event.endTime = Time()
}

VoteEventStruct function GetVoteEvent( int id )
{
	return file.events[ id ]
}

// shared util functions //

void function Vote_EasyReleaseFunc( VoteEventStruct voteEvent )
{
	Chat_ServerBroadcast( "\n\x1b[32m一个新的投票ID为 \x1b[36m"+ voteEvent.id +"\x1b[32m 的名为 \x1b[36m"+ voteEvent.voteName +"\x1b[32m 的投票已被发布", false )
	Chat_ServerBroadcast( "\x1b[32m输入 \x1b[36m!vote 投票ID y \x1b[32m投同意票\n输入 \x1b[36m!vote 投票ID n \x1b[32m投反对票", false )
}

bool function Vote_EasyVoteFunc( VoteEventStruct voteEvent, entity player, bool vote )
{
	if( voteEvent.hasVotedPlayers.contains( player.GetUID() ) )
	{
		Chat_ServerPrivateMessage( player, "\x1b[31m你已经投过票了！", false, false )
		return true
	}
	voteEvent.hasVotedPlayers.append( player.GetUID() )
	if( vote )
		voteEvent.voteYesCount += 1
	else
		voteEvent.voteNoCount += 1

	Chat_ServerBroadcast( "\n\x1b[32m投票ID为 \x1b[36m"+ voteEvent.id +"\x1b[32m 的名为 \x1b[36m"+ voteEvent.voteName +"\x1b[32m 的投票目前票数为\n\x1b[36m同意票 "+ voteEvent.voteYesCount +" / "+ voteEvent.voteNoCount +" 反对票", false )
	if( Vote_ShouldEarlyEnd( voteEvent ) )
	{
		EndVoteEvent( voteEvent, eVoteEndCode.complete )
		return true
	}

	Chat_ServerBroadcast( "\x1b[32m输入 \x1b[36m!vote 投票ID y \x1b[32m投同意票\n输入 \x1b[36m!vote 投票ID n \x1b[32m投反对票", false )
	return true
}

void function Vote_EasyEndFunc( VoteEventStruct voteEvent )
{
	if( voteEvent.endCode == eVoteEndCode.timeout )
		Chat_ServerBroadcast( "\n\x1b[32m名为 \x1b[36m"+ voteEvent.voteName +"\x1b[32m 的投票目前票数为\n\x1b[36m同意票 "+ voteEvent.voteYesCount +" / "+ voteEvent.voteNoCount +" 反对票", false )
	string voteEndText = "\x1b[33m此投票已结束！投票结果为："
	int yes = voteEvent.voteYesCount
	int no = voteEvent.voteNoCount
	if( yes == no )
		voteEndText += "\x1b[36m票数持平，按照反对票居多处理"
	else if( yes > no )
		voteEndText += "\x1b[36m同意票居多"
	else
		voteEndText += "\x1b[36m反对票居多"
	Chat_ServerBroadcast( voteEndText, false )

	voteEvent.callbackFunc( yes > no )
}

bool function Vote_ShouldEarlyEnd( VoteEventStruct voteEvent )
{
	if( voteEvent.voteYesCount > float( GetPlayerArray().len() ) / 2 )
		return true
	if( voteEvent.voteNoCount >= float( GetPlayerArray().len() ) / 2 )
		return true
	if( voteEvent.voteYesCount + voteEvent.voteNoCount == GetPlayerArray().len() )
		return true
	return false
}