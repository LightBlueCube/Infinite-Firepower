untyped
global function PilotShield_Init

void function PilotShield_Init()
{
    RegisterSignal( "StartButtonCancelCheck" )
    RegisterSignal( "StopUseShieldBattery" )
    RegisterSignal( "DoneUseShieldBattery" )

	AddCallback_OnClientConnected( OnClientConnected )
	AddCallback_PlayerClassChanged( OnPlayerClassChanged )
	AddDamageCallback( "player", OnPlayerDamaged )
}

void function OnClientConnected( entity player )
{
	if( !KillStreakEnable() )
		return

	AddPlayerHeldButtonEventCallback( player, IN_OFFHAND4, TryUseShieldBattery, 0.0 )
}

void function OnPlayerDamaged( entity victim, var damageInfo )
{
	if( victim.GetShieldHealth() == 0 || !victim.IsHuman() )
		return

	float damage = DamageInfo_GetDamage( damageInfo )
	if( damage <= 0 )
		return

	DamageInfo_AddCustomDamageType( damageInfo, DF_SHIELD_DAMAGE )

	if( damage >= victim.GetShieldHealth() )
	{
		DamageInfo_SetDamage( damageInfo, damage - victim.GetShieldHealth() )
		victim.SetShieldHealth( 0 )
		StatusEffect_AddTimed( victim, eStatusEffect.emp, 1.0, 2.0, 1.0 )
		EmitSoundOnEntity( victim, "titan_energyshield_down" )
		if( victim.s.GUIClose )
			SendHudMessage( victim, "护盾损坏\n0/"+ victim.GetShieldHealthMax(), -1, -0.4, 255, 100, 100, 255, 0, 1.0, 1.0 )
		return
	}
	StatusEffect_AddTimed( victim, eStatusEffect.emp, GraphCapped( victim.GetShieldHealth(), victim.GetShieldHealthMax(), 0, 0.1, 1.0 ), 2.0, 1.0 )
	EmitSoundOnEntityOnlyToPlayer( victim, victim, "titanshieldwall_light_bulletimpact_1p_vs_3p" )
	if( victim.s.GUIClose )
		SendHudMessage( victim, "护盾受击\n"+ max( 0, victim.GetShieldHealth() - damage ) +"/"+ victim.GetShieldHealthMax(), -1, -0.4, 255, 255, 100, 255, 0, 1.0, 1.0 )
}

void function OnPlayerClassChanged( entity player )
{
	if( !KillStreakEnable() )
		return

	// health&shield update
	if ( !IsAlive( player ) ) // can't set health for a no-alive entity
		return
	if ( !player.IsTitan() && player.GetPlayerSettings() != "spectator" )
	{
		player.SetShieldHealthMax( 100 )
		player.SetShieldHealth( 100 )
	}
}

void function TryUseShieldBattery( entity player )
{
	player.EndSignal( "OnDestroy" )
	player.EndSignal( "OnDeath" )
	entity activeWeapon = player.GetActiveWeapon()
	wait 0.5

	player.Signal( "StopUseShieldBattery" )
	player.Signal( "SendingNewGUI" )
	player.EndSignal( "StopUseShieldBattery" )
	entity battery = GetBatteryOnBack( player )

	if( player.GetShieldHealth() == player.GetShieldHealthMax() || !IsValid( battery ) )
	{
		player.HolsterWeapon()
		player.DeployWeapon()
		if( IsValid( player.GetOffhandWeapon( OFFHAND_INVENTORY ) ) )
			if( player.GetOffhandWeapon( OFFHAND_INVENTORY ).GetWeaponInfoFileKeyField( "fire_mode" ) == "offhand_hybrid" )
				if( IsValid( activeWeapon ) && IsAlive( player ) )
					player.SetActiveWeaponByName( activeWeapon.GetWeaponClassName() )
		return
	}

	battery.EndSignal( "OnDestroy" )

	table results = {}
	results.canceledUsage <- true // if player cancels usage it will run more functions

	int statusEffectHandle = StatusEffect_AddEndless( player, eStatusEffect.move_slow, 0.3 )

	OnThreadEnd(
		function(): ( player, battery, statusEffectHandle, activeWeapon, results )
		{
			if( !IsValid( player ) )
				return

			player.s.GUIDisable = false

			player.DeployWeapon()
			player.Server_TurnOffhandWeaponsDisabledOff()
			if( IsValid( player.GetOffhandWeapon( OFFHAND_INVENTORY ) ) )
				if( player.GetOffhandWeapon( OFFHAND_INVENTORY ).GetWeaponInfoFileKeyField( "fire_mode" ) == "offhand_hybrid" )
					if( IsValid( activeWeapon ) && IsAlive( player ) )
						player.SetActiveWeaponByName( activeWeapon.GetWeaponClassName() )

			StopSoundOnEntity( player, "Weapon_EnergySyphon_Charge_1P" )
			StatusEffect_Stop( player, statusEffectHandle )

			if( !results.canceledUsage )
				return
			if( !IsAlive( player ) )
				return
			if( !IsValid( battery ) )
				return
			if( !PlayerHasMaxBatteryCount( player ) )
				return

			entity newestBattery = Rodeo_TakeBatteryAwayFromPilot( player )
			if( IsValid( battery ) && IsValid( newestBattery ) )
			{
				if( battery == newestBattery )
					Rodeo_PilotPicksUpBattery( player, battery )
				else
					Rodeo_PilotPicksUpBattery( player, newestBattery )
			}
		}
	)
	player.s.GUIDisable = true

	bool isAmped = IsAmpedBattery( battery )
	if( isAmped )
		SendHudMessage( player, "正在取出电池...\n", -1, -0.4, 255, 255, 100, 255, 0, 1.0, 0.0 )
	else
		SendHudMessage( player, "正在取出电池...\n", -1, -0.4, 100, 255, 100, 255, 0, 1.0, 0.0 )
	float useTime = isAmped ? 1.5 : 1.0

	player.HolsterWeapon()
	player.Server_TurnOffhandWeaponsDisabledOn()
	wait 0.6 // wait for player hoster their weapon
	battery.ClearParent()
	battery.SetParent( player, "PROPGUN" )
	battery.SetAngles( < 0,90,90 > )
	EmitSoundOnEntityOnlyToPlayer( player, player, "Weapon_EnergySyphon_Charge_1P" )

	float startTime = Time()
	while( startTime + useTime > Time() )
	{
		player.HolsterWeapon() // defensive fix
		player.Server_TurnOffhandWeaponsDisabledOn()
		if( isAmped )
			SendHudMessage( player, "正在使用电池  松开按键以取消\n[" + CreateHudProgressBarThroughTime( Time(), startTime, useTime ) + "]", -1, -0.4, 255, 255, 100, 255, 0, 0.2, 0 )
		else
			SendHudMessage( player, "正在使用电池  松开按键以取消\n[" + CreateHudProgressBarThroughTime( Time(), startTime, useTime ) + "]", -1, -0.4, 100, 255, 100, 255, 0, 0.2, 0 )

		if( !PlayerHasMaxBatteryCount( player ) )
			return
		WaitFrame()
	}
	if( !PlayerHasMaxBatteryCount( player ) )
		return

	// done using
	player.Signal( "DoneUseShieldBattery" )
	results.canceledUsage = false
	player.SetShieldHealth( player.GetShieldHealthMax() )
	if( isAmped )
		SendHudMessage( player, "护盾恢复\n"+ player.GetShieldHealth() +"/"+ player.GetShieldHealthMax(), -1, -0.4, 255, 255, 100, 255, 0, 1.0, 1.0 )
	else
		SendHudMessage( player, "护盾恢复\n"+ player.GetShieldHealth() +"/"+ player.GetShieldHealthMax(), -1, -0.4, 100, 255, 100, 255, 0, 1.0, 1.0 )

	entity shieldFXHandle = StartParticleEffectOnEntity_ReturnEntity( player, GetParticleSystemIndex( $"P_xo_armor_body_CP" ), FX_PATTACH_POINT_FOLLOW, player.LookupAttachment( "CHESTFOCUS" ) )

	SetTeam( shieldFXHandle, player.GetTeam() )
	shieldFXHandle.SetOwner( player )

	if( isAmped )
		EffectSetControlPointVector( shieldFXHandle, 1, < 255, 165, 0 > )
	else
		EffectSetControlPointVector( shieldFXHandle, 1, < 30, 255, 100 > )

	shieldFXHandle.kv.VisibilityFlags = ENTITY_VISIBLE_TO_ENEMY | ENTITY_VISIBLE_TO_FRIENDLY

	MessageToPlayer( player, eEventNotifications.Rodeo_PilotAppliedBatteryToYou, player, isAmped )

	entity newestBattery = Rodeo_TakeBatteryAwayFromPilot( player )
	if( IsValid( battery ) && IsValid( newestBattery ) )
	{
		if( battery == newestBattery )
			newestBattery.Destroy()
	}
}

string function CreateHudProgressBarThroughTime( float nowGlobalTime, float startGlobalTime, float totalTime )
{
	int sysbolNum = int( GraphCapped( nowGlobalTime, startGlobalTime, startGlobalTime + totalTime, 0, 32 ) ) + 1
	string text = ""
	for( int i = sysbolNum; i > 0; i-- )
		text += "/"
	for( int i = 32 - sysbolNum; i > 0; i-- )
		text += "-"
	return text
}